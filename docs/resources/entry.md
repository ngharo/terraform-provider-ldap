---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "ldap_entry Resource - ldap"
subcategory: ""
description: |-
  Manages an LDAP entry. Each entry is identified by its Distinguished Name (DN) and contains attributes.
  Managing Attributes: Null vs Empty Lists
  The ldap_entry resource supports three ways to handle LDAP attributes, each with different behavior:
  Managed Attributes (Non-null values)
  When you specify an attribute with a value (including an empty list []), the provider actively manages that attribute.
  Unmanaged Attributes (Null values)
  When you set an attribute to null, the provider does not manage that attribute, but it will read and refresh the value from the LDAP server.
  Important Limitation: During resource creation, null attributes appear as null in state. Actual values from the LDAP server appear after the next terraform refresh or terraform plan.
  Omitted Attributes
  When you don't include an attribute in the configuration at all, the provider will not read or manage it.
---

# ldap_entry (Resource)

Manages an LDAP entry. Each entry is identified by its Distinguished Name (DN) and contains attributes.

## Managing Attributes: Null vs Empty Lists
The `ldap_entry` resource supports three ways to handle LDAP attributes, each with different behavior:

### Managed Attributes (Non-null values)
When you specify an attribute with a value (including an empty list `[]`), the provider **actively manages** that attribute.

### Unmanaged Attributes (Null values)
When you set an attribute to `null`, the provider **does not manage** that attribute, but it will **read and refresh** the value from the LDAP server.

**Important Limitation:** During resource creation, null attributes appear as `null` in state. Actual values from the LDAP server appear after the next `terraform refresh` or `terraform plan`.

### Omitted Attributes
When you don't include an attribute in the configuration at all, the provider will **not read or manage** it.

## Example Usage

```terraform
# Create a basic person entry
resource "ldap_entry" "user" {
  dn = "cn=john.doe,ou=users,dc=example,dc=com"
  attributes = {
    objectClass = ["person", "organizationalPerson", "inetOrgPerson"]
    cn          = ["john.doe"]
    sn          = ["Doe"]
    givenName   = ["John"]
    mail        = ["john.doe@example.com"]
    description = ["Software Engineer"]
  }
}

# Create a group entry
resource "ldap_entry" "group" {
  dn = "cn=developers,ou=groups,dc=example,dc=com"
  attributes = {
    objectClass = ["top", "groupOfNames"]
    cn          = ["developers"]
    description = ["Development team"]
    member = [
      ldap_entry.user.dn,
      "cn=admin,ou=users,dc=example,dc=com",
    ]
  }
}

# Create an organizational unit
resource "ldap_entry" "department" {
  dn = "ou=engineering,dc=example,dc=com"
  attributes = {
    objectClass = ["top", "organizationalUnit"]
    ou          = ["engineering"]
    description = ["Engineering Department"]
  }
}

locals {
  // userAccountControl tags
  // Enforcment based on the sum of tags
  // https://learn.microsoft.com/en-us/troubleshoot/windows-server/active-directory/useraccountcontrol-manipulate-account-properties
  tags = {
    ACCOUNTDISABLE                 = 2
    HOMEDIR_REQUIRED               = 8
    LOCKOUT                        = 16
    PASSWD_NOTREQD                 = 32
    PASSWD_CANT_CHANGE             = 64
    ENCRYPTED_TEXT_PWD_ALLOWED     = 128
    TEMP_DUPLICATE_ACCOUNT         = 256
    NORMAL_ACCOUNT                 = 512
    INTERDOMAIN_TRUST_ACCOUNT      = 2048
    WORKSTATION_TRUST_ACCOUNT      = 4096
    SERVER_TRUST_ACCOUNT           = 8192
    DONT_EXPIRE_PASSWORD           = 65536
    MNS_LOGON_ACCOUNT              = 131072
    SMARTCARD_REQUIRED             = 262144
    TRUSTED_FOR_DELEGATION         = 524288
    NOT_DELEGATED                  = 1048576
    USE_DES_KEY_ONLY               = 2097152
    DONT_REQ_PREAUTH               = 4194304
    PASSWORD_EXPIRED               = 8388608
    TRUSTED_TO_AUTH_FOR_DELEGATION = 16777216
  }
}
# Example: Active Directory user with unicodePwd
# The provider automatically encodes unicodePwd as UTF-16LE with quotes
resource "ldap_entry" "ad_user" {
  dn = "CN=AD User,OU=Users,DC=example,DC=com"
  attributes = {
    objectClass        = ["top", "person", "organizationalPerson", "user"]
    cn                 = ["AD User"]
    sAMAccountName     = ["aduser"]
    userPrincipalName  = ["aduser@example.com"]
    accountExpires     = ["0"]
    userAccountControl = [tostring(local.tags.NORMAL_ACCOUNT + local.tags.DONT_EXPIRE_PASSWORD)]
  }

  # unicodePwd is automatically encoded as UTF-16LE for Active Directory
  attributes_wo = {
    unicodePwd = ["Password123!"]
  }

  # Increment this version to rotate the password
  attributes_wo_version = 1
}

# Example: Group with null member attribute for external management
# This creates a group where membership is managed by external systems
# (e.g., scripts, AD tools, other automation) but Terraform can read the current members
resource "ldap_entry" "ad_group" {
  dn = "CN=AppUsers,OU=Groups,DC=example,DC=com"
  attributes = {
    objectClass = ["top", "group"]
    cn          = ["AppUsers"]
    description = ["Application users - membership managed externally"]
    member      = null # Not managed by Terraform, but will be read from LDAP
  }
}

# Reference the externally-managed member list in outputs or other resources
output "current_members" {
  value = ldap_entry.ad_group.attributes.member
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `attributes` (Map of List of String) Map of LDAP attributes for the entry. The keys are attribute names and values are lists of attribute values. Null values are not managed by Terraform but read from LDAP on refresh. Empty list values will be actively managed by Terraform.
- `dn` (String) The distinguished name (DN) of the LDAP entry. This uniquely identifies the entry in the LDAP directory tree. Changing this forces a new resource to be created.

### Optional

> **NOTE**: [Write-only arguments](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments) are supported in Terraform 1.11 and later.

- `attributes_wo` (Map of List of String, [Write-only](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments)) Write-only map of LDAP attributes for the entry containing sensitive values. Use this for sensitive data like passwords, API keys, or other secrets. Must be used in conjunction with `attributes_wo_version`. Requires Terraform 1.11 or later. NOTE: `unicodePwd` will be automatically encoded as UTF-16LE for Active Directory.
- `attributes_wo_version` (Number) Version number for write-only attributes. Increment this value (e.g., 1, 2, 3) whenever you want to update the `attributes_wo` values on the LDAP server. Since write-only attributes are not stored in state, Terraform cannot detect changes to them. Changing this version number triggers the provider to send the current `attributes_wo` values to the LDAP server during updates.

### Read-Only

- `id` (String) The unique identifier for this resource, which is the same as the DN.

## Import

Import is supported using the following syntax:

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can be used, for example:

```shell
#!/bin/bash
# Simple DN (default - imports only objectClass):
terraform import ldap_entry.user "CN=user,OU=Users,DC=example,DC=com"

# JSON with specific attributes:
terraform import ldap_entry.user '{"dn": "CN=user,OU=Users,DC=example,DC=com", "attributes": ["objectClass", "cn", "sAMAccountName",
  "userPrincipalName"]}'
```
